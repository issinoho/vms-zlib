$! ZLIB$STARTUP.COM - system startup for zlib on OpenVMS
$!
$! Installed by PCSI into SYS$STARTUP.  Defines the system logical name
$! ZLIB$ROOT, pointing at the installed [ZLIB] directory, so that programs
$! compile with /INCLUDE=ZLIB$ROOT:[INCLUDE] and link with
$! ZLIB$ROOT:[LIB]LIBZ.OLB/LIBRARY.  To run it at every boot, add this
$! line to SYS$MANAGER:SYSTARTUP_VMS.COM:
$!
$!     $ @SYS$STARTUP:ZLIB$STARTUP.COM
$!
$! P1 = "INSTALL": also print the post-installation tasks (PCSI runs it so).
$! P1 = "REMOVE":  deassign ZLIB$ROOT instead (PCSI runs it so at removal).
$!
$ set noon
$ mode = f$edit(p1, "UPCASE")
$ if mode .eqs. "REMOVE"
$ then
$   if f$trnlnm("ZLIB$ROOT", "LNM$SYSTEM_TABLE") .nes. "" then -
        deassign/system/executive_mode ZLIB$ROOT
$   exit 1
$ endif
$!
$! This procedure sits in <destination>[SYS$STARTUP]; the product is in
$! <destination>[ZLIB].  Rooted logicals need the physical form:
$! DKA0:[SYS0.SYSCOMMON.SYS$STARTUP] -> DKA0:[SYS0.SYSCOMMON.ZLIB.]
$ proc = f$environment("PROCEDURE")
$ dev = f$parse(proc,,,"DEVICE","NO_CONCEAL")
$ dir = f$edit(f$parse(proc,,,"DIRECTORY","NO_CONCEAL"), "UPCASE") - "]["
$ root = dir - "SYS$STARTUP]" + "ZLIB.]"
$ if root .eqs. dir + "ZLIB.]"
$ then
$   write sys$error "ZLIB$STARTUP: expected to be in a [SYS$STARTUP] directory, not ''dir'"
$   exit 44
$ endif
$ root = root - ".000000"
$ define/system/executive_mode/translation_attributes=concealed ZLIB$ROOT 'dev''root'
$ if f$search("ZLIB$ROOT:[LIB]LIBZ.OLB") .eqs. ""
$ then
$   write sys$error "ZLIB$STARTUP: LIBZ.OLB not found under ''dev'''root'"
$   exit 44
$ endif
$ if mode .nes. "INSTALL" then exit 1
$ say = "write sys$output"
$ say ""
$ say "    Post-installation tasks for zlib"
$ say ""
$ say "    At system startup: to define ZLIB$ROOT at every boot, add this line to"
$ say "    SYS$MANAGER:SYSTARTUP_VMS.COM:"
$ say "    $ @SYS$STARTUP:ZLIB$STARTUP.COM"
$ say "    Building against zlib: compile with"
$ say "    /INCLUDE=ZLIB$ROOT:[INCLUDE]"
$ say "    and link with ZLIB$ROOT:[LIB]LIBZ.OLB/LIBRARY."
$ say "    See ZLIB$ROOT:[DOC]README.VMS."
$ say ""
$ say "    PRODUCT REMOVE ZLIB removes the product and deassigns ZLIB$ROOT."
$ say ""
$ exit 1
